/*
 * Copyright (c) 2023, WSO2 LLC. (http://www.wso2.org)
 *
 * WSO2 LLC. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'io.ballerina.plugin'
}

description = 'Ballerina - Mongodb Ballerina Connector'

def packageName = "mongodb"
def packageOrg = "ballerinax"
def tomlVersion = stripBallerinaExtensionVersion("${project.version}")
def ballerinaTomlFilePlaceHolder = new File("${project.rootDir}/build-config/resources/Ballerina.toml")
def ballerinaTomlFile = new File("$project.projectDir/Ballerina.toml")

def stripBallerinaExtensionVersion(String extVersion) {
    if (extVersion.matches(project.ext.timestampedVersionRegex)) {
        def splitVersion = extVersion.split('-')
        if (splitVersion.length > 3) {
            def strippedValues = splitVersion[0..-4]
            return strippedValues.join('-')
        } else {
            return extVersion
        }
    } else {
        return extVersion.replace("${project.ext.snapshotVersion}", "")
    }
}

ballerina {
    packageOrganization = packageOrg
    module = packageName
    testCoverageParam = "--code-coverage --coverage-format=xml"
    platform = "java21"
}

configurations {
    externalJars
}

dependencies {
    externalJars(group: 'org.mongodb', name: 'mongodb-driver-sync', version: "${mongoDriverVersion}") {
        transitive = false
    }
    externalJars(group: 'org.mongodb', name: 'mongodb-driver-core', version: "${mongoDriverVersion}") {
        transitive = false
    }
    externalJars(group: 'org.mongodb', name: 'bson', version: "${mongoDriverVersion}") {
        transitive = false
    }
}

task updateTomlFiles {
    doLast {
        def newBallerinaToml = ballerinaTomlFilePlaceHolder.text.replace("@project.version@", project.version)
        newBallerinaToml = newBallerinaToml.replace("@toml.version@", tomlVersion)
        newBallerinaToml = newBallerinaToml.replace("@mongodb.driver.version@", mongoDriverVersion)
        ballerinaTomlFile.text = newBallerinaToml
    }
}

task commitTomlFiles {
    doLast {
        project.exec {
            ignoreExitValue true
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "git commit -m \"[Automated] Update the toml files\" Ballerina.toml Dependencies.toml"
            } else {
                commandLine 'sh', '-c', "git commit -m '[Automated] Update the toml files' Ballerina.toml Dependencies.toml"
            }
        }
    }
}

task startMongodbServer() {
    doLast {
        println("Starting mongodb server...")
        exec {
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "docker compose -f tests/resources/docker/docker-compose.yml up && exit %%ERRORLEVEL%%"
            } else {
                commandLine 'sh', '-c', "docker compose -f tests/resources/docker/docker-compose.yml up -d"
            }
        }
    }
}

task stopMongodbServer() {
    doLast {
        println("Stopping mongodb server...")
        exec {
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "docker compose -f tests/resources/docker/docker-compose.yml down && exit %%ERRORLEVEL%%"
            } else {
                commandLine 'sh', '-c', "docker compose -f tests/resources/docker/docker-compose.yml down"
            }
        }
    }
}

clean {
    delete 'build'
    delete 'lib'
}

build.dependsOn copyToLib
build.dependsOn ":${packageName}-native:build"
build.dependsOn startMongodbServer
build.finalizedBy stopMongodbServer

test.dependsOn ":${packageName}-native:build"
test.dependsOn startMongodbServer
test.finalizedBy stopMongodbServer

publish.dependsOn build
publishToMavenLocal.dependsOn build
